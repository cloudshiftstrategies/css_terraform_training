# HCL 101

* The syntax of Terraform configurations is called HashiCorp Configuration Language (HCL).
* HCL is meant to strike a balance between human readable and editable as well as being machine-friendly.
* HCL describes all resouces managed by terraform

## HCL project files

* A single project's Terraform HCL files all live in a single directory (excluding modules which we'll cover later)

* Terraform HCL files must end in either .tf or .tf.json to specify the format that is in use (HCL or json respectively).

* Files in project directory not suffix'd in *.tf or *.tf.json are ignored.

* Multiple file formats can be present in the same directory; (it is okay to have one Terraform configuration file be
    Terraform syntax and another be JSON.)
    
Example terraform project directory
```hcl-terraform
.
├── file.tf.json
├── main.tf
├── outputs.tf
└── vnet.tf
```
## HCL Load order

When invoking any command that loads the Terraform configuration,
* Terraform loads all configuration files within the directory specified in alphabetical order.
* The configuration within the loaded files are appended to each other.
* **The order of variables, resources, etc. defined within the configuration doesn't matter.**

## Terraform HCL Key concepts

#### [Resources](https://www.terraform.io/docs/configuration/resources.html)

The most important thing you'll configure with Terraform are resources.
    
Resources are a component of your infrastructure. (server, email provider, DNS record, or database)
    
The resource block creates a resource of the given TYPE (first parameter) and NAME (second parameter). The
combination of the type and name must be unique.

Within the { } block  is configuration for the resource. The configuration is dependent on the type, and
is documented for each resource type in the providers section.
    
Resources can also have dependancies, timeouts, multiple provisioners, multiple counts and more. See 
[Resouces Documentation](https://www.terraform.io/docs/configuration/resources.html) for more details
    
Example of a basic Resource
 ```hcl-terraform
resource "aws_instance" "web" {
  ami           = "ami-408c7f28"
  instance_type = "t2.micro"
}
```
    
#### [Variables](https://www.terraform.io/docs/configuration/variables.html)

Input variables serve as parameters for a Terraform module.
For infrastructure to become truly shareable and version controlled, we need to parameterize the configurations.

Each variable block configures a single input variable for a Terraform module. 
The name given in the variable block header is used to reference the variable 

Within the block body (between { }) is configuration for the variable, which accepts the following arguments:

 * type (Optional) - Valid values are string, list, and map. Default is string

 * default (Optional) - This sets a default value for the variable. 
 
    If no default is provided, Terraform will raise an error if a value is not provided by the caller.

 * description (Optional) - A human-friendly description for the variable. This is primarily for documentation
 
Example of a basic variable
```hcl-terraform
# AMI variable
variable "my_ami" {
  type    = "string"
  default = "ami-408c7f28"
  description = "The AMI # that we'll deploy"
}

resource "aws_instance" "web" {
  ami           = "${var.my_ami}"
  instance_type = "t2.micro"
}
```

Variables can be set using default, environment variriables, CLI arguments or tfvars files
* to set a variable using os environment variables prefix the variable name with TF_VAR_ `export TF_VAR_my_ami=ami-12345`
* to set a variable on the terraform command line  `terraform apply -var my_ami=ami-12345`
* to set a variable using a [xxx.tfvars file](https://www.terraform.io/docs/configuration/variables.html#variable-files)
  `terraform apply -var-file=xxx.tfvars`


## [Local Values](https://www.terraform.io/docs/configuration/locals.html)
Local values are like variables, but allow them to be assigned with an expression.

Example locals block
```hcl-terraform
# Require that a variable with project name be set
variable "project_name" {
  description = "The name of our project"
}

# Create a local that is a combination of var.project_name and "-web"
locals {
  name_prefix = "${var.project_name}-web"
}

# Deploy an AWS instance with a name tag that includes the name prefix
resource "aws_instance" "web" {
  ami           = "${var.my_ami}"
  instance_type = "t2.micro"
  tags {
    "Name" = "${local.name_prefix}-instance"
  }
}
```

### [Providers](https://www.terraform.io/docs/configuration/providers.html)
Providers are responsible in Terraform for managing the lifecycle of a resource: create, read, update, delete.

Provider are usually open source and community developed. Providers include AWS, Azure, Vmware, palo alto and dozens
more. See the full list of public providers [here](https://www.terraform.io/docs/providers/index.html) 

Most providers require some sort of configuration to provide authentication information, endpoint URLs, etc.

By default, resources are matched with provider configurations by matching the start of the resource name. For example,
a resource of type vsphere_virtual_machine is associated with a provider called vsphere.

Each time a new provider is added to configuration -- either explicitly via a provider block or by adding a resource
from that provider -- it's necessary to initialize that provider before use. Initialization downloads and installs
the provider's plugin and prepares it to be used.

Example provider block
```hcl-terraform
provider "aws" {
  access_key = "XLKJALKHDAKJ@KJDW"
  secret_key = "alskdjwe;fkewlkfjew;lfkjwefo2n"
  region     = "us-east-1"
}
```


### [Outputs](https://www.terraform.io/docs/configuration/outputs.html)

Outputs define values that will be highlighted to the user when Terraform applies

Outputs are also used to share data between terraform projects/workspaces

Outputs are used to provide data back to caller from modules

Example output block
```hcl-terraform
output "inst_public_dns" {
  value = "${aws_instance.db.public_dns}"
}
```

### [Data Sources](https://www.terraform.io/docs/configuration/data-sources.html)
Data sources allow data to be imported, fetched or computed from a remote configuration or provider.

Use of data sources allows a Terraform configuration to build on information defined outside of Terraform, or defined
by another separate Terraform configuration.

Example data resource
```hcl-terraform
# Find the latest available ubuntu AMI
data "aws_ami" "web" {
  filter {
    name   = "name"
    values = ["*ubuntu-bionic-18.04-amd64-server-*"]
  }
  most_recent = true
}

# Deploy an AWS instance using the latest ubuntu AMI
resource "aws_instance" "web" {
  ami           = "${data.aws_ami.web}"
  instance_type = "t2.micro"
  }
```

### [Modules](https://www.terraform.io/docs/configuration/modules.html)
Modules are used in Terraform to modularize and encapsulate groups of resources in your infrastructure.

A module block instructs Terraform to create an instance of a module, and in turn to instantiate any resources defined
within it.

The name given in the block header is used to reference the particular module instance 

All attributes within the block must correspond to variables within the module, with the exception of the following
    which Terraform treats as special:

 * source - (Required) A module source string specifying the location of the child module source code.
 * version - (Optional) A version constraint that specifies which versions of the module are acceptable. 
 * providers - (Optional) A map whose keys are provider configuration names
 
 Modules can located in the [terraform public module registry](https://registry.terraform.io/), they can be on the 
 local filesystem, or they can be from the 
 [terraform enterprise private module registry](https://www.terraform.io/docs/enterprise/registry/index.html)
 
 ```hcl-terraform
# example of a public module
module "pub_sg" {
  source  = "terraform-aws-modules/security-group/aws"
  version = "2.10.0"
  name = "my_sg"
  vpc_id = "vpc-123456"
}

# Example of a local module
moudule "lcl_sg" {
  souce = "../my_securitygroup_module"
  name = "my_sg"
  vpc_id = "vpc-123456"
}

# Example of a private module hosted in terraform enterprise
module "pvt_sg" {
  source  = "app.terraform.io/cloudshiftstrategies/terraform-aws-securitygroup-module"
  version = "1.4"
  name = "my_sg"
  vpc_id = "vpc-123456"
}

```

### [Provisioners](https://www.terraform.io/docs/provisioners/index.html)
Provisioners are used to execute scripts on a local or remote machine as part of resource creation or destruction.

Provisioners can be used to bootstrap a resource, cleanup before destroy, run configuration management, etc.

Provisioners can be shell scripts, chef, remote exec commands, file puts and several other types described
[here](https://www.terraform.io/docs/provisioners/index.html)

* Creation-Time Provisioners run when the resource they are defined within is created. Creation-time provisioners are only
run during creation, not during updating or any other lifecycle. They are meant as a means to perform bootstrapping of
a system.

* Destroy-Time Provisioners run before the resource is destroyed.

Example provisioner
```hcl-terraform
resource "aws_instance" "web" {
  # ...

  # push a script to the instance servers
  provisioner "file" {
    source      = "script.sh"
    destination = "/tmp/script.sh"
  }
  # the run the script on the instance
  provisioner "remote-exec" {
    inline = [
      "chmod +x /tmp/script.sh",
      "/tmp/script.sh args",
    ]
  }
}
```

## HCL Syntax

* Single line comments start with #
```hcl-terraform
# This is an HCL comment
resource "aws_instance" "my_instance" {...}
```
* Multi-line comments are wrapped with /* and */
```hcl-terraform
/* This is a HCL 
multi
line
comment */
resource "aws_instance" "my_instance" {...}
```
* Values are assigned with the syntax of key = value (whitespace doesn't matter).
    * The value can be any primitive (string, number, boolean), a list, or a map.
```hcl-terraform
resource "resource_type" "resource_name" {
  string_paramter = "ami-1234567"
  number_parameter       =     99
  bool_parameter=true 
  list_parameter = ["string1", "string2"]
  map_parameter = {"key1":"value", "key2": "value2"}
}
```
* Strings are in double-quotes "
```hcl-terraform
resource "resource_type" "resource_name" {
  string_paramter = "ami-1234567"
}
```

* Embedded within strings in Terraform, whether you're using the Terraform syntax or JSON syntax, you can interpolate
other values. These interpolations are wrapped in **"${}"**, such as "${var.foo}"

```hcl-terraform
variable "foo" {
  default = "bar"
}
resource "ex_resource_type" "my_resource" {
  interpolated_paramter = "${var.foo}"
}
```
* Multiline strings can use shell-style "here doc" syntax: <<EOF 
```hcl-terraform
variable "foo" {
  default = <<EOF
multi
line
string
EOF
  description = "A multi-line string"
}
```
* Numbers are not quoted & assumed to be base 10. If you prefix a number with 0x, it is treated as a hexadecimal number.
```hcl-terraform
baseten_number = 10
hex_number = 0xf

```
* Boolean values: true, false. (NOT: "true" "false")
```hcl-terraform
bool_true = true
bool_false = false
```
* Lists (arrays) of primitive types can be made with square brackets []
```hcl-terraform
variable "list" {
  type = "list"
  default = ["foo", "bar", 10, 0xf]
}
```
* Maps (dictionaries) can be made with braces {}
    * Quotes may be omitted on keys, unless the key starts with a number, in which case quotes are required.
    * Commas are required between key/value pairs for single line maps.
    * A newline between key/value pairs is sufficient in multi-line maps.
 ```hcl-terraform
variable "singleline_map" {
  type = "map"
  default = {foo = "bar", "2foo" = "2bar", nofoo = 0}
}

variable "multiline_map" {
  type = "map"
  default = {
      foo = "bar"
      "2foo" = "2bar"
      nofoo = 0
  }
}
```

[Back to Main page](../README.md)

[Next page - hcl201](hcl201.md)
